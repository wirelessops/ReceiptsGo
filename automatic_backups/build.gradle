plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

android {
    compileSdk = libs.versions.compileSdk.get().toInteger()


    defaultConfig {
        minSdk = libs.versions.minSdk.get().toInteger()
        targetSdk = libs.versions.targetSdk.get().toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        missingDimensionStrategy("isFloss", "notFloss")
    }

    buildTypes {
        release {
            minifyEnabled(false)
            proguardFiles(
                    getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        targetCompatibility = JavaVersion.VERSION_11
        sourceCompatibility = JavaVersion.VERSION_11
    }
    kotlin {
        jvmToolchain(11)
    }
    flavorDimensions "isFloss"
    packagingOptions {
        resources {
            excludes += ["META-INF/DEPENDENCIES"]
        }
    }


    productFlavors {
        floss {
            dimension "isFloss"
        }

        notFloss {
            dimension "isFloss"
        }
    }
    namespace "com.wops.automatic_backups"

}

dependencies {
    implementation(project(":core"))
    implementation(project(":wbMiniLibrary"))

    implementation(libs.com.hadisatrio.optional)
    implementation(libs.com.google.apis.api.services.drive) {
        exclude group: "org.apache.httpcomponents"
    }

    notFlossImplementation("com.google.android.gms:play-services-auth:$GOOGLE_AUTH_VERSION")

    notFlossImplementation(libs.google.api.client.android) {
        exclude group: "org.apache.httpcomponents"
    }

    notFlossImplementation(libs.com.google.http.client.gson)

    // Unit Tests
    testImplementation(libs.org.robolectric)
    testImplementation(libs.junit)
    testImplementation(libs.org.mockito.core)
    testImplementation(libs.com.nhaarman.mockitokotlin2.mockito.kotlin)
    testImplementation(libs.androidx.test.core)
}
